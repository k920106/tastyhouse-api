plugins {
    id 'java-library'
    id 'idea'
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.mysql:mysql-connector-j'
    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    api 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation"
}

// QueryDSL 생성 파일을 build 폴더에 생성하도록 설정
def querydslDir = "$buildDir/generated/sources/annotationProcessor/java/main"
def querydslTestDir = "$buildDir/generated/sources/annotationProcessor/java/test"

sourceSets {
    main {
        java {
            srcDirs += querydslDir
        }
    }
    test {
        java {
            srcDirs += querydslTestDir
        }
    }
}

tasks.named('compileJava') {
    options.generatedSourceOutputDirectory = file(querydslDir)
}

tasks.named('compileTestJava') {
    options.generatedSourceOutputDirectory = file(querydslTestDir)
}

// IntelliJ IDEA 설정: .apt_generated 폴더 대신 build 폴더 사용
idea {
    module {
        generatedSourceDirs += file(querydslDir)
        generatedSourceDirs += file(querydslTestDir)
    }
}

// This module should not have a main method, so disable bootJar task
jar {
    enabled = true
    archiveClassifier = ''
}

bootJar {
    enabled = false
}
